# Day 5 第五周学习小结

#### 王玥 2016011693 工物60

1. **量化策略的完善和检验**

   股票池+择时=策略基础

2. **股票池设计思路**

   + 市值因子

   + 估值因子：PE(TTM)

     > TTM（Trailing Twelve Months），是一种滚动概念，表示截止当前，最近12个月或近4个季度的财报数据。引入TTM的意义在于剔除财务分析上的季节性（seasonality）变化，使得分析更加严谨、符合逻辑。

   + 超跌因子

   + ST股票的处理

   剔除法：一些指标无法准确选择更好的股票，但可以剔除更差的。

3. **择时信号设计**

   + K线级别：N分钟K线、日线、周线、月线、年线

   + 时间周期：MA5、MA30、MA60

   练习：把策略择时信号的K线时间级别改为30分钟、60分钟，看回测结果变化。

   方法：修改参数：PL_TRADE_BAR_DURATION；

   以30分钟为例：

![](E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\30.png)

4. **趋势性策略**：

   入场信号相对于出场信号，出场信号更重要

5. **股票池进一步优化**：

+ 选股条件，再平衡，容量

+ 多因子模型：因子尽量少；因子间尽量独立；时间尽可能长。

——多财务因子：

| 类型           | 财务指标                     |
| -------------- | ---------------------------- |
| 风险           | 资产负债率；流动比率         |
| 运营效率       | 总资产周转率；存货周转率     |
| 盈利能力及质量 | 净资产收益率；销售收现比率   |
| 前景与评估     | 营业收入增长率；净利润增长率 |

+ 择时信号进一步优化：

实战part：小周期大均线：

在小周期上工作，但长时均线需要较大以反映整体趋势；

关注点：尽早在趋势开始时进入；避免因为假信号错误入场；

> 在尽量小的时间级别，以获得足够多的信号，及时进入，如20minK线，但5min错误信号又过多；故一个不错的选择：MA3穿MA200。

6. **均值回复型策略**：

+ 趋势跟随与均值回复

+ 小幅震荡vs.大幅趋势

+ 缺口式：

  缺口分为：普通缺口、突破缺口、持续性缺口、消耗性缺口、除权缺口

  > + **跳空缺口**：
  >
  > *跳空缺口*是指股的开盘价高于昨天的最高价或低于昨天的最低价,使K线图出现空档的现象。假定昨天收盘点位1000点，今天开盘1020点，全天始终在1020点上方运行，就是一个完整的20点*跳空缺口*；如果当天最低点低于1000点，只能叫跳高开盘，缺口已经弥补。

<img src="E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\缺口.png" style="zoom: 67%;" />

套利模式：

+ 两只股票之间-完美替代品（如：平安+人寿）；

+ 两组股票之间-测定相关性；

+ 2种或者多种ETF之间;
+ ETF与一揽子股票之间；
+ 现货与期货之间。

7. **TB学习**：

TB：适用于期货；回测方便。

**海龟交易示例代码：**

```c
//------------------------------------------------------------------------
// 简称: TurtleTrader
// 名称: 海龟交易系统
// 类别: 公式应用
// 类型: 内建应用
//------------------------------------------------------------------------

Params
    Numeric RiskRatio(1);                   // % Risk Per N ( 0 - 100)
    Numeric ATRLength(20);                  // 平均波动周期 ATR Length
    Numeric boLength(20);                   // 短周期 BreakOut Length
    Numeric fsLength(55);                   // 长周期 FailSafe Length
    Numeric teLength(10);                   // 离市周期 Trailing Exit Length
    Bool LastProfitableTradeFilter(True);   // 使用入市过滤条件
Vars
    Numeric MinPoint;                       // 最小变动单位
    NumericSeries AvgTR;                    // ATR
    Numeric N;                              // N 值
    Numeric TotalEquity;                    // 按最新收盘价计算出的总资产
    Numeric TurtleUnits;                    // 交易单位
    NumericSeries DonchianHi;               // 唐奇安通道上轨，延后1个Bar
    NumericSeries DonchianLo;               // 唐奇安通道下轨，延后1个Bar
    NumericSeries fsDonchianHi;             // 唐奇安通道上轨，延后1个Bar，长周期
    NumericSeries fsDonchianLo;             // 唐奇安通道下轨，延后1个Bar，长周期
    Numeric ExitHighestPrice;               // 离市时判断需要的N周期最高价
    Numeric ExitLowestPrice;                // 离市时判断需要的N周期最低价
    Numeric myEntryPrice;                   // 开仓价格
    Numeric myExitPrice;                    // 平仓价格
    Bool SendOrderThisBar(False);           // 当前Bar有过交易
    NumericSeries preEntryPrice(0);         // 前一次开仓的价格
    BoolSeries PreBreakoutFailure(false);   // 前一次突破是否失败
Begin
    // 集合竞价过滤
    If(!CallAuctionFilter()) Return;

    If(BarStatus == 0)
    {
        preEntryPrice = InvalidNumeric;
        PreBreakoutFailure = false;
    }

    MinPoint = MinMove*PriceScale;
    AvgTR = XAverage(TrueRange,ATRLength);
    N = AvgTR[1];
    TotalEquity = Portfolio_CurrentCapital() + Portfolio_UsedMargin();
    TurtleUnits = (TotalEquity*RiskRatio/100) /(N * ContractUnit()*BigPointValue());
    TurtleUnits = IntPart(TurtleUnits); // 对小数取整

    DonchianHi = HighestFC(High[1],boLength);
    DonchianLo = LowestFC(Low[1],boLength);

    fsDonchianHi = HighestFC(High[1],fsLength);
    fsDonchianLo = LowestFC(Low[1],fsLength);

    ExitLowestPrice = LowestFC(Low[1],teLength);
    ExitHighestPrice = HighestFC(High[1],teLength);

    Commentary("N="+Text(N));
    Commentary("preEntryPrice="+Text(preEntryPrice));
    Commentary("PreBreakoutFailure="+IIFString(PreBreakoutFailure,"True","False"));

    // 当不使用过滤条件，或者使用过滤条件并且条件为PreBreakoutFailure为True进行后续操作
    If(MarketPosition == 0 && ((!LastProfitableTradeFilter) Or (PreBreakoutFailure)))
    {
        // 突破开仓
        If(High > DonchianHi && TurtleUnits >= 1)
        {
            // 开仓价格取突破上轨+一个价位和最高价之间的较小值，这样能更接近真实情况，并能尽量保证成交
            myEntryPrice = min(high,DonchianHi + MinPoint);
            myEntryPrice = IIF(myEntryPrice < Open, Open,myEntryPrice); // 大跳空的时候用开盘价代替
            preEntryPrice = myEntryPrice;
            Buy(TurtleUnits,myEntryPrice);
            SendOrderThisBar = True;
            PreBreakoutFailure = False;
        }

        If(Low < DonchianLo && TurtleUnits >= 1)
        {
            // 开仓价格取突破下轨-一个价位和最低价之间的较大值，这样能更接近真实情况，并能尽量保证成交
            myEntryPrice = max(low,DonchianLo - MinPoint);
            myEntryPrice = IIF(myEntryPrice > Open, Open,myEntryPrice); // 大跳空的时候用开盘价代替
            preEntryPrice = myEntryPrice;
            SendOrderThisBar = True;
            SellShort(TurtleUnits,myEntryPrice);
            SendOrderThisBar = True;
            PreBreakoutFailure = False;
        }
    }

    // 长周期突破开仓 Failsafe Breakout point
    If(MarketPosition == 0)
    {
        Commentary("fsDonchianHi="+Text(fsDonchianHi));
        If(High > fsDonchianHi && TurtleUnits >= 1)
        {
            // 开仓价格取突破上轨+一个价位和最高价之间的较小值，这样能更接近真实情况，并能尽量保证成交
            myEntryPrice = min(high,fsDonchianHi + MinPoint);
            myEntryPrice = IIF(myEntryPrice < Open, Open,myEntryPrice); // 大跳空的时候用开盘价代替
            preEntryPrice = myEntryPrice;
            Buy(TurtleUnits,myEntryPrice);
            SendOrderThisBar = True;
            PreBreakoutFailure = False;
        }

        Commentary("fsDonchianLo="+Text(fsDonchianLo));
        If(Low < fsDonchianLo && TurtleUnits >= 1)
        {
            // 开仓价格取突破下轨-一个价位和最低价之间的较大值，这样能更接近真实情况，并能尽量保证成交
            myEntryPrice = max(low,fsDonchianLo - MinPoint);
            myEntryPrice = IIF(myEntryPrice > Open, Open,myEntryPrice); // 大跳空的时候用开盘价代替
            preEntryPrice = myEntryPrice;
            SellShort(TurtleUnits,myEntryPrice);
            SendOrderThisBar = True;
            PreBreakoutFailure = False;
        }
    }

    If(MarketPosition == 1) // 有多仓的情况
    {
        Commentary("ExitLowestPrice="+Text(ExitLowestPrice));
        If(Low < ExitLowestPrice)
        {
            myExitPrice = max(Low,ExitLowestPrice - MinPoint);
            myExitPrice = IIF(myExitPrice > Open, Open,myExitPrice); // 大跳空的时候用开盘价代替
            Sell(0,myExitPrice);    // 数量用0的情况下将全部平仓
        }Else
        {
            If(preEntryPrice!=InvalidNumeric && TurtleUnits >= 1)
            {
                If(Open >= preEntryPrice + 0.5*N) // 如果开盘就超过设定的1/2N,则直接用开盘价增仓。
                {
                    myEntryPrice = Open;
                    preEntryPrice = myEntryPrice;
                    Buy(TurtleUnits,myEntryPrice);
                    SendOrderThisBar = True;
                }

                while(High >= preEntryPrice + 0.5*N) // 以最高价为标准，判断能进行几次增仓
                {
                    myEntryPrice = preEntryPrice + 0.5 * N;
                    preEntryPrice = myEntryPrice;
                    Buy(TurtleUnits,myEntryPrice);
                    SendOrderThisBar = True;
                }
            }

            // 止损指令
            If(Low <= preEntryPrice - 2 * N && SendOrderThisBar == false) // 加仓Bar不止损
            {
                myExitPrice = preEntryPrice - 2 * N;
                myExitPrice = IIF(myExitPrice > Open, Open,myExitPrice); // 大跳空的时候用开盘价代替
                Sell(0,myExitPrice); // 数量用0的情况下将全部平仓
                PreBreakoutFailure = True;
            }
        }
    }Else If(MarketPosition ==-1) // 有空仓的情况
    {
        // 求出持空仓时离市的条件比较值
        Commentary("ExitHighestPrice="+Text(ExitHighestPrice));
        If(High > ExitHighestPrice)
        {
            myExitPrice = Min(High,ExitHighestPrice + MinPoint);
            myExitPrice = IIF(myExitPrice < Open, Open,myExitPrice); // 大跳空的时候用开盘价代替
            BuyToCover(0,myExitPrice);    // 数量用0的情况下将全部平仓
        }Else
        {
            If(preEntryPrice!=InvalidNumeric && TurtleUnits >= 1)
            {
                If(Open <= preEntryPrice - 0.5*N) // 如果开盘就超过设定的1/2N,则直接用开盘价增仓。
                {
                    myEntryPrice = Open;
                    preEntryPrice = myEntryPrice;
                    SellShort(TurtleUnits,myEntryPrice);
                    SendOrderThisBar = True;
                }

                while(Low <= preEntryPrice - 0.5*N) // 以最低价为标准，判断能进行几次增仓
                {
                    myEntryPrice = preEntryPrice - 0.5 * N;
                    preEntryPrice = myEntryPrice;
                    SellShort(TurtleUnits,myEntryPrice);
                    SendOrderThisBar = True;
                }
            }

            // 止损指令
            If(High >= preEntryPrice + 2 * N &&SendOrderThisBar==false) // 加仓Bar不止损
            {
                myExitPrice = preEntryPrice + 2 * N;
                myExitPrice = IIF(myExitPrice < Open, Open,myExitPrice); // 大跳空的时候用开盘价代替
                BuyToCover(0,myExitPrice); // 数量用0的情况下将全部平仓
                PreBreakoutFailure = True;
            }
        }
    }
End

//------------------------------------------------------------------------
// 编译版本 GS2010.12.08
// 版权所有 TradeBlazer Software 2003－2010
// 更改声明 TradeBlazer Software保留对TradeBlazer平
//          台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
```

**练习1**：风险因子调整为账户总资金的2%；短期突破系统改为25日；长期突破系统改为66日；退出突破系统改为8日；计算ATR的周期改为22日。

修改参数之前组合报告：

![](E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\海龟01结果.png)

修改参数之后组合报告：

![](E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\海龟练习1.png)

**练习2**：

（1）交易滑点：

> 所谓的滑点，是指在进行交易时，客户下达的指定交易点位与实际交易点位存在较大差别的一种现象。也就是说，当实际成交价格与起始报价不一样时，便会产生滑点（①无滑点；②正向滑点；③负向滑点）。

+ 滑点产生原因：
  + 市场报价断层（但是在行情剧烈波动或者出现大额直接进出的时候，就会出现价格的断层）；
  + 网络延迟（例如在非农、美联储公布利率决议时行情波动剧烈的时候，就很容易出现滑点较大的现象）。

+ 控制滑点：限价命令；市场范围命令。
+ 设置交易滑点的办法：
+ 设置交易滑点的原因：

（2）反向突破退出时如何限制开仓与加仓bar不退出：

inside bar:

> Inside bar是指一个K线（或者一系列K线）完全被之前的K线所包含，之前的这根K线叫做mother bar。Inside bar应该比mother bar有更低的高点和更高的低点（一些交易者使用更加宽松的定义，也包括两根K线相等的情况）。在较小的时间框架内，例如1小时图表，日线图表中的inside bar在1小时周期内看起来就像是K线的三角形模式。
>
> <img src="E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\motherbar.png" style="zoom:50%;" />
>
> **重要提示**：由于inside bar设置本质上是一个潜在的突破信号，因此我只能在mother bar的高点或低点突破处选择入场。如果我想做多，我会在mother bar高点上方突破时买入，如果我想做空，我会在mother bar低点下方突破卖出。

<img src="E:\我的学习文档\2020秋季学期\金融大数据与量化分析\第五周\insidebar.png" style="zoom:80%;" />



（3）课后思考：

+ 怎样限制1根bar内只能加仓一次

+ 示例代码哪些地方不适合实盘



8. **SQL学习**：

+ Data models & the relationship data model

+ Schemas & data independence

  inner join 返回的是非空的，outer join可能返回空的

```python
import sqlite3
import random
import string
db=sqlite3.connect("univeristy.db")
cursor=db.cursor()
cursor.execute('''CREATE TABLE IF NOT EXIST students (
    id integer PRIMARY KEY,
    name text NOT NULL,
    class text NOT NULL,
    grade integer);''')
cursor.execute("""INSERT INTO students (id,name,class,grade)
    VALUES(1,Mary,"Python",100)""")
db.commit()
newID=input("Enter ID number；")
newName=input("Enter name:")
newClass=input("Enter class:")
newGrade=input("Enter grade:")
cursor.execute("""INSERT INTO students (id,name,class,grade)
    VALUES(?,?,?,?)""",(newID,newName,newClass,newClass))
db.commit()
cursor.execute("SELECT * FROM students")
print(cursor.fetchall())
db.close()
```

